<?xml version="1.0"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
         xmlns:s="library://ns.adobe.com/flex/spark"
         xmlns:list="de.dittner.siegmar.view.common.list.*"
         implements="de.dittner.siegmar.view.fileView.file.IFileContent">

    <fx:Script><![CDATA[
        import de.dittner.siegmar.model.domain.fileSystem.body.links.BookLink;
        import de.dittner.siegmar.model.domain.fileSystem.body.links.BookLinksBody;
        import de.dittner.siegmar.model.domain.fileSystem.file.SiegmarFile;
        import de.dittner.siegmar.view.common.list.FileBodyList;

        import mx.collections.ArrayCollection;
        import mx.collections.Sort;

        //--------------------------------------
        //  bookLinksBody
        //--------------------------------------
        private var _bookLinksBody:BookLinksBody;
        [Bindable("bookLinksBodyChanged")]
        public function get bookLinksBody():BookLinksBody {return _bookLinksBody;}
        public function set bookLinksBody(value:BookLinksBody):void {
            if (_bookLinksBody != value) {
                _bookLinksBody = value;
                dispatchEvent(new Event("bookLinksBodyChanged"));
            }
        }

        //--------------------------------------
        //  fileBodyList
        //--------------------------------------
        public function get fileBodyList():FileBodyList {return list;}

        //--------------------------------------
        //  file
        //--------------------------------------
        private var fileChanged:Boolean = false;
        private var _file:SiegmarFile;
        [Bindable("fileChanged")]
        public function get file():SiegmarFile {return _file;}
        public function set file(value:SiegmarFile):void {
            if (_file != value) {
                _file = value;
                fileChanged = true;
                invalidateProperties()
                dispatchEvent(new Event("fileChanged"));
            }
        }

        override protected function commitProperties():void {
            super.commitProperties();
            if (fileChanged) {
                fileChanged = false;
                if (file) {
                    var coll:ArrayCollection = new ArrayCollection((file.body as BookLinksBody).bookLinks);
                    coll.filterFunction = filterFunction;
                    coll.sort = getLinksSort();
                    list.dataProvider = coll;
                }
            }
        }

        private static var linksSort:Sort;
        public static function getLinksSort():Sort {
            if (!linksSort) {
                linksSort = new Sort();
                linksSort.compareFunction = linkCompareFunction;
            }
            return linksSort;
        }

        private static function linkCompareFunction(a:BookLink, b:BookLink, fields:Array = null):int {
            var result:int = 0;
            if (a.authorName > b.authorName) {
                result = 1;
            }
            else if (a.authorName == b.authorName) {
                if (a.bookName > b.bookName) result = 1;
                else if (a.bookName == b.bookName) result = 0;
                else result = -1;
            }
            else result = -1;

            return result;
        }

        private var filterText:String = "";
        public function filterNotes(txt:String):void {
            filterText = txt;
            if (list && list.dataProvider)
                (list.dataProvider as ArrayCollection).refresh();
        }

        private function filterFunction(item:Object):Boolean {
            if (!filterText) return true;
            return item is BookLink ? (item as BookLink).toHtmlText().toLowerCase().indexOf(filterText) != -1 : true;
        }
        ]]></fx:Script>

    <s:Scroller width="100%" height="100%"
                styleName="cosmoScrollerStyle"
                horizontalScrollPolicy="off" verticalScrollPolicy="on">
        <list:FileBodyList id="list"
                           width="100%"
                           bookLinksBody="{bookLinksBody}"
                           dragEnabled="false"
                           clickableArea="40"
                           deselectEnabled="true"
                           itemRenderer="de.dittner.siegmar.view.fileView.file.bookLinks.BookLinkRenderer">
            <list:layout>
                <s:VerticalLayout gap="0" useVirtualLayout="true"/>
            </list:layout>
        </list:FileBodyList>
    </s:Scroller>

</s:Group>
